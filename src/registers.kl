PROGRAM registers
%NOLOCKGROUP

%INCLUDE errors.klt

%INCLUDE registers.klt
%INCLUDE registers.klh
%INCLUDE errors.klh

BEGIN
END registers

--private
ROUTINE get_type_num (typ : STRING) : INTEGER
  BEGIN
    IF (typ='R') THEN RETURN(DATA_REG) ; ENDIF
    IF (typ='PR') THEN RETURN(DATA_POSREG) ; ENDIF
    IF (typ='SR') THEN RETURN(DATA_STRING) ; ENDIF
    IF (typ='F') THEN RETURN(io_flag) ; ENDIF
    IF (typ='DI') THEN RETURN(io_din) ; ENDIF
    IF (typ='DO') THEN RETURN(io_dout) ; ENDIF
    IF (typ='AI') THEN RETURN(io_anin) ; ENDIF
    IF (typ='AO') THEN RETURN(io_anout) ; ENDIF
    IF (typ='RI') THEN RETURN(io_rdi) ; ENDIF
    IF (typ='RO') THEN RETURN(io_rdo)  ; ENDIF
    IF (typ='GI') THEN RETURN(io_gpin) ; ENDIF
    IF (typ='GO') THEN RETURN(io_gpout)  ; ENDIF
    IF (typ='UI') THEN RETURN(io_uopin) ; ENDIF
    IF (typ='UO') THEN RETURN(io_uopout) ; ENDIF
    IF (typ='SI') THEN RETURN(io_sopin) ; ENDIF
    IF (typ='SO') THEN RETURN(io_sopout) ; ENDIF

    RETURN(0)
  END get_type_num

-- #########
-- Getters
-- #########

-- return comments from registers and IO's 
-- specifying the register type and its number
ROUTINE registers__get_comment
  ------------------------------------
  VAR
    port_type : INTEGER
    cmt : STRING[30]
    STATUS : INTEGER
  BEGIN
    port_type = get_type_num(typ)

    IF port_type = 0 THEN
      karelError(POS_TYPE_MISMATCH,'GET_CMT-wrong type', ER_ABORT)
      RETURN('')
    ENDIF

    SELECT port_type OF
      CASE(DATA_REG):
        GET_REG_CMT(id, cmt, STATUS)
      CASE(DATA_POSREG):
        GET_PREG_CMT(id, cmt, STATUS)
      CASE(DATA_STRING):
        GET_SREG_CMT(id, cmt, STATUS)
      ELSE:
        GET_PORT_CMT(port_type, id, cmt, STATUS)
    ENDSELECT

    CHK_STAT(STATUS)

    RETURN(cmt)
  END registers__get_comment

ROUTINE registers__get_io
  VAR
    port_type : INTEGER
    STATUS : INTEGER
    val : INTEGER
  BEGIN
    port_type = get_type_num(typ)

    IF (port_type = 0) OR (port_type = DATA_REG) OR (port_type = DATA_POSREG) OR &
       (port_type = DATA_STRING) THEN
      karelError(POS_TYPE_MISMATCH,'GET_PORT_VAL wrong type', ER_ABORT)
      RETURN(0)
    ENDIF

    GET_PORT_VAL(port_type, port_no, val, STATUS)
    CHK_STAT(STATUS)

    RETURN(val)
  END registers__get_io

-- Public: get integer value for specified register
-- wrapper for GET_REG
-- returns the integer value of the register
ROUTINE registers__get_int
  VAR
    real_flg     :  BOOLEAN
    real_val    :  REAL
    int_val      : INTEGER
    STATUS   : INTEGER
  BEGIN
    GET_REG(reg_no, real_flg, int_val, real_val, STATUS)
    CHK_STAT(STATUS)
    IF real_flg = TRUE THEN
      int_val = TRUNC(real_val)
    ENDIF
      
    RETURN(int_val)
  END registers__get_int

-- Get real register value
-- Arg: real register number
ROUTINE registers__get_real
  VAR
    real_flg    :  BOOLEAN
    real_val   :  REAL
    int_val     : INTEGER
    STATUS : INTEGER
  BEGIN
    GET_REG(reg_no, real_flg, int_val, real_val, STATUS)
    CHK_STAT(STATUS)
    IF real_flg = FALSE THEN
      real_val = int_val
    ENDIF
      
    RETURN(real_val)
  END registers__get_real


-- Used for retrieving any number register if unsure of type
-- return a boolean if number is a real number, returns false
-- if number is an integer
ROUTINE registers__get_numreg
  VAR
    real_flg : BOOLEAN
    STATUS : INTEGER
  BEGIN
    real_val = 0.0
    int_val = 0
    GET_REG(reg_no, real_flg, int_val, real_val, STATUS)
    CHK_STAT(STATUS)
    RETURN(real_flg)
  END registers__get_numreg

-- Get string register value
ROUTINE registers__get_string
  VAR
    str_val	: STRING[100]
    STATUS : INTEGER
  BEGIN
    GET_STR_REG(reg_no, str_val, STATUS)
    CHK_STAT(STATUS)
    
    RETURN(str_val)
  END registers__get_string

-- #########
-- Setters
-- #########

-- set string
-- input SR number "reg_no"
-- input string "str_val"
ROUTINE registers__set_string
  VAR
    STATUS : INTEGER
  BEGIN
    SET_STR_REG(reg_no, str_val, STATUS)
    CHK_STAT(STATUS)
    IF STATUS <> 0 THEN
      ok = FALSE
    ELSE
      ok = TRUE
    ENDIF 
  END registers__set_string

-- Set the comment of a Register or IO
-- input "typ" - type of Register or IO
-- input "Id" - register or IO number
-- input "cmt" - set comment
ROUTINE registers__set_comment
  VAR
    port_type : INTEGER
    STATUS : INTEGER
  BEGIN

    port_type = get_type_num(typ)

    IF port_type = 0 THEN
      karelError(POS_TYPE_MISMATCH,'SET_CMT-wrong type', ER_ABORT)
      ok = FALSE
    ENDIF

    SELECT port_type OF
      CASE(DATA_REG):
        SET_REG_CMT(id, cmt, STATUS)
      CASE(DATA_POSREG):
        SET_PREG_CMT(id, cmt, STATUS)
      CASE(DATA_STRING):
        SET_SREG_CMT(id, cmt, STATUS)
      ELSE:
        SET_PORT_CMT(port_type, id, cmt, STATUS)
    ENDSELECT

    CHK_STAT(STATUS)
    ok = TRUE
    
  END registers__set_comment

ROUTINE registers__set_int
  VAR
    STATUS : INTEGER
  BEGIN
    SET_INT_REG(reg_no, int_val, STATUS)
    CHK_STAT(STATUS)
    IF STATUS <> 0 THEN
      ok = FALSE
    ELSE
      ok = TRUE
    ENDIF
  END registers__set_int

ROUTINE registers__set_real
  VAR
    STATUS : INTEGER
  BEGIN
    SET_REAL_REG(reg_no, real_val, STATUS)
    CHK_STAT(STATUS)
    IF STATUS <> 0 THEN
      ok = FALSE
    ELSE
      ok = TRUE
    ENDIF
  END registers__set_real

-- #########
-- Checks
-- #########

ROUTINE registers__is_real
  VAR
    real_flg    :  BOOLEAN
      real_val   :  REAL
      int_val     : INTEGER
      STATUS : INTEGER
  BEGIN
    GET_REG(reg_no, real_flg, int_val, real_val, STATUS)
      CHK_STAT(STATUS)
    IF STATUS <> 0 THEN
      ok = FALSE
    ELSE
      ok = TRUE
    ENDIF

    RETURN(real_flg)
  END registers__is_real

ROUTINE registers__is_int
  VAR
    real_flg    :  BOOLEAN
      real_val   :  REAL
      int_val     : INTEGER
      STATUS : INTEGER
  BEGIN
    GET_REG(reg_no, real_flg, int_val, real_val, STATUS)
      CHK_STAT(STATUS)
    IF STATUS <> 0 THEN
      ok = FALSE
    ELSE
      ok = TRUE
    ENDIF
    
    RETURN(NOT real_flg)
  END registers__is_int


-- #########
-- Conversions
-- #########
